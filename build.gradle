plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.google.cloud.tools.jib' version '3.4.4'
}

group = 'com.donggi-lee-bit'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	runtimeOnly 'mysql:mysql-connector-java:8.0.33'
	runtimeOnly 'com.h2database:h2'
	compileOnly 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.2'
	testImplementation 'io.rest-assured:rest-assured'
	testImplementation 'io.rest-assured:json-path'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
}

jib {
	from {
		image = "eclipse-temurin:21-alpine"
	}
	extraDirectories {
		paths {
			path {
				from = file('src/main/jib/pinpoint-agent')
				into = '/app/libs/pinpoint-agent'
			}
		}
	}
	container {
		ports = ["8080"]
		jvmFlags = [
			"-Xms512m",
			"-Xmx1024m",
			"-javaagent:/app/libs/pinpoint-agent/pinpoint-bootstrap-3.0.1.jar",
			"-Dspring.profiles.active=${System.getenv('ACTIVE_PROFILE')}",
			"-Dprofiler.transport.grpc.collector.ip=${System.getenv('PINPOINT_COLLECTOR_IP')}",
			"-Dpinpoint.config=/app/libs/pinpoint-agent/pinpoint-root.config",
			"-Dpinpoint.agentId=${System.getenv('PINPOINT_AGENT_ID')}",
			"-Dpinpoint.applicationName=${System.getenv('PINPOINT_APP_NAME')}"
		]
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
